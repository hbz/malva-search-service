plugins {
    id "org.sonarqube" version "2.2"
    id 'nebula.ospackage' version '4.0.0'
}

printf "Host: %s\nOS: %s %s %s\nJVM: %s %s %s %s\nGroovy: %s\nGradle: %s\n" +
        "Build: group: ${project.group} name: ${project.name} version: ${project.version}\n",
        InetAddress.getLocalHost(),
        System.getProperty("os.name"),
        System.getProperty("os.arch"),
        System.getProperty("os.version"),
        System.getProperty("java.version"),
        System.getProperty("java.vm.version"),
        System.getProperty("java.vm.vendor"),
        System.getProperty("java.vm.name"),
        GroovySystem.getVersion(),
        gradle.gradleVersion

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'nebula.rpm'

// for web browser testing
import org.apache.tools.ant.taskdefs.condition.Os

apply from: "gradle/ext.gradle"
apply from: "gradle/downloads.gradle"

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets {
    integrationTest {
        groovy {
            srcDir file('src/integration-test/groovy')
            compileClasspath += main.output + test.output
        }
        resources {
            srcDir file('src/integration-test/resources')
        }
    }
}

configurations {
    alpnboot
    wagon
    distJars
    integrationTestCompile {
        extendsFrom testCompile
    }
    integrationTestRuntime {
        extendsFrom testRuntime
    }
}

dependencies {
    compile "org.xbib.malva:malva-elasticsearch:${project.property('malva.version')}"
    compile "org.xbib:cql:${project.property('xbib-cql.version')}"
    compile "org.xbib:content-xml:${project.property('xbib-content.version')}"
    compile "com.fasterxml.woodstox:woodstox-core:${project.property('woodstox.version')}"
    compile "xerces:xercesImpl:${project.property('xerces.version')}"
    compile "xalan:xalan:${project.property('xalan.version')}"
    compileOnly "org.codehaus.groovy:groovy-all:${project.property('groovy.version')}"
    testCompile "org.apache.logging.log4j:log4j-1.2-api:${project.property('log4j.version')}"
    testCompile "org.apache.logging.log4j:log4j-jul:${project.property('log4j.version')}"
    testCompile "org.apache.logging.log4j:log4j-jcl:${project.property('log4j.version')}"
    testCompile "junit:junit:${project.property('junit.version')}"
    testCompile "org.spockframework:spock-core:${project.property('spock.version')}"
    integrationTestCompile "org.gebish:geb-junit4:${project.property('geb.version')}"
    integrationTestCompile "org.seleniumhq.selenium:selenium-chrome-driver:${project.property('selenium.version')}"
    integrationTestCompile "org.seleniumhq.selenium:selenium-firefox-driver:${project.property('selenium.version')}"
    integrationTestCompile("com.codeborne:phantomjsdriver:${project.property('phantomjsdriver.version')}") {
        // phantomjs driver pulls in a different selenium version
        transitive = false
    }
    wagon 'org.apache.maven.wagon:wagon-ssh-external:2.10'
    // openssl native libs are optional and must be added to integration-test classpath separately
    if ('os x' == org.gradle.internal.os.OperatingSystem.current().getFamilyName()) {
        integrationTestCompile "io.netty:netty-tcnative:${project.property('tcnative.version')}:osx-x86_64"
    }
    if ('linux' == org.gradle.internal.os.OperatingSystem.current().getFamilyName()) {
        if (new File("/etc/redhat-release").exists()) {
            // use this for linking to libssl.so.10 (RHEL/Fedora/CentOS)
            integrationTestCompile "io.netty:netty-tcnative:${project.property('tcnative.version')}:linux-x86_64-fedora"
        } else {
            // use this for linking to libssl.so.1.0.0
            integrationTestCompile "io.netty:netty-tcnative:${project.property('tcnative.version')}:linux-x86_64"
        }
    }
    alpnboot "org.mortbay.jetty.alpn:alpn-boot:${project.property('alpnboot.version')}"
    distJars("org.elasticsearch:elasticsearch:${project.property('elasticsearch.version')}") {
        // dependencies that are overriden by org.xbib deps
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.dataformat'
        exclude group: 'com.google.guava'
        // dependencies that are not for client
        exclude module: 'org.elasticsearch:securesm'
        // we use log4j2, not log4j
        exclude group: 'log4j'
    }
    // Elasticsearch add-ons
    distJars "com.vividsolutions:jts:${project.property('jts.version')}"
    distJars "com.github.spullara.mustache.java:compiler:${project.property('mustache.version')}"
    // indy
    distJars "org.codehaus.groovy:groovy-all:${project.property('groovy.version')}:indy"
    distJars "org.xbib.malva:malva:${project.property('malva.version')}"
    distJars "org.xbib.malva:malva-elasticsearch:${project.property('malva.version')}"
    // better netty performance
    distJars "org.javassist:javassist:${project.property('javassist.version')}"
    // netty 4.1 needs alpn for http/2
    distJars "org.mortbay.jetty.alpn:alpn-boot:${project.property('alpnboot.version')}"
    // additional packages for search service
    distJars "org.xbib:cql:${project.property('xbib-cql.version')}"
    distJars "org.xbib:content-xml:${project.property('xbib-content.version')}"
    distJars "com.fasterxml.woodstox:woodstox-core:${project.property('woodstox.version')}"
    distJars "xerces:xercesImpl:${project.property('xerces.version')}"
    distJars "xalan:xalan:${project.property('xalan.version')}"
    // log bridges
    distJars "org.apache.logging.log4j:log4j-jcl:${project.property('log4j.version')}"
    distJars "org.apache.logging.log4j:log4j-jul:${project.property('log4j.version')}"
}

compileGroovy {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}

test {
    testLogging {
        showStandardStreams = false
        exceptionFormat = 'full'
    }
}

task integrationTest(type: Test) {
    // note: bootstrapClasspath does not use /p (or /a)
    jvmArgs "-Xbootclasspath/p:" + configurations.alpnboot.asPath
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = configurations.integrationTestCompile
    classpath += sourceSets.main.output
    classpath += sourceSets.test.output
    classpath += sourceSets.integrationTest.output
    outputs.upToDateWhen { false }
    systemProperty 'log4j.configurationFile', projectDir.absolutePath + '/src/test/resources/log4j2.xml'
    systemProperty 'java.util.logging.manager','org.apache.logging.log4j.jul.LogManager'
    systemProperty 'webapp.bind.uri', 'http://${net.hostname}:9500'
    systemProperty 'webapp.publish.uri', 'http://${net.hostname}:9500'
    testLogging.showStandardStreams = true
}

drivers.each { driver ->
    task "${driver}IntegrationTest"(type: Test, dependsOn: ['jar']) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = configurations.integrationTestCompile
        classpath += sourceSets.main.output
        classpath += sourceSets.test.output
        classpath += sourceSets.integrationTest.output
        outputs.upToDateWhen { false }
        systemProperty 'log4j.configurationFile', projectDir.absolutePath + '/src/test/resources/log4j2.xml'
        reports {
            junitXml.destination = file("$buildDir/test-results/${driver}")
        }
        systemProperty 'geb.build.reportsDir', reporting.file("${driver}/geb")
        systemProperty 'geb.env', driver
        testLogging.showStandardStreams = false
    }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

integrationTest.mustRunAfter test
check.dependsOn integrationTest

firefoxIntegrationTest {
    mustRunAfter test
}

chromeIntegrationTest {
    mustRunAfter test
    dependsOn unzipChromeDriver
    def chromedriverFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "chromedriver.exe" : "chromedriver"
    systemProperty "webdriver.chrome.driver", new File(unzipChromeDriver.outputs.files.singleFile, chromedriverFilename).absolutePath
}

phantomJsIntegrationTest {
    mustRunAfter test
    dependsOn unzipPhantomJs
    def phantomJsFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "phantomjs.exe" : "bin/phantomjs"
    systemProperty "phantomjs.binary.path", new File(unzipPhantomJs.outputs.files.singleFile, phantomJsFilename).absolutePath
}

clean {
    delete 'logs'
    delete 'sessions'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
}

groovydoc {
    docTitle =  description
}

task groovydocJar(type: Jar, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives sourcesJar, javadocJar, groovydocJar
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}

apply from: 'gradle/linux.gradle'
apply from: 'gradle/publish.gradle'
apply from: 'gradle/sonarqube.gradle'
