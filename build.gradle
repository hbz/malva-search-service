plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id "org.sonarqube" version "2.1-rc1"
    id 'org.ajoberstar.github-pages' version '1.6.0-rc.1'
    id "org.xbib.gradle.plugin.jbake" version "1.1.0"
    id 'nebula.ospackage' version '4.0.0'
}

println "Host: " + java.net.InetAddress.getLocalHost()
println "Gradle: " + gradle.gradleVersion + " JVM: " + org.gradle.internal.jvm.Jvm.current() + " Groovy: " + GroovySystem.getVersion()
println "Build: group: '${project.group}', name: '${project.name}', version: '${project.version}'"
println "Timestamp: " + java.time.Instant.now().atZone(java.time.ZoneId.systemDefault()).toString()

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.ajoberstar.github-pages'
apply plugin: 'org.xbib.gradle.plugin.jbake'
apply plugin: 'nebula.rpm'

// for web browser testing
import org.apache.tools.ant.taskdefs.condition.Os

ext {
    user = 'xbib'
    name = 'groovy-webapp-library-search'
    description = 'Groovy Web app library search'
    mainClassname = 'org.xbib.webapp.bootstrap.Bootstrap'
    scmUrl = 'https://github.com/' + user + '/' + name
    scmConnection = 'scm:git:git://github.com/' + user + '/' + name + '.git'
    scmDeveloperConnection = 'scm:git:git://github.com/' + user + '/' + name + '.git'
    versions = [
            'groovy': '2.4.7',
            'groovy-webapp' : '1.0.0',
            'xerces' : '2.11.0',
            'xalan' : '2.7.2',
            'woodstox' : '5.0.2',
            'elasticsearch' : '2.2.1',
            'netty': '4.1.4.Final',
            'tcnative': '1.1.33.Fork19',
            'alpnboot': '8.1.9.v20160720',
            'junit' : '4.12',
            'geb': '0.13.1',
            'selenium': '2.52.0',
            'chrome': '2.10',
            'phantomJs':  '1.9.7'
    ]
    drivers = ["firefox", "chrome", "phantomJs"]
}
apply from: "gradle/downloads.gradle"

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url "http://xbib.org/repository"
    }
    maven {
        url "http://repo.gradle.org/gradle/repo"
    }
}

sourceSets {
    integrationTest {
        groovy {
            srcDir file('src/integration-test/groovy')
            compileClasspath += main.output + test.output
        }
        resources {
            srcDir file('src/integration-test/resources')
        }
    }
}

configurations {
    provided
    alpnboot
    wagon
    distJars
    integrationTestCompile {
        extendsFrom testCompile
    }
    integrationTestRuntime {
        extendsFrom testRuntime
    }
}

dependencies {
    compile 'org.xbib.groovy:groovy-webapp:' + versions.'groovy-webapp'
    compile "org.xbib:content-xml:1.0.0"
    compile "org.xbib:cql:2.0.0"
    compile 'com.fasterxml.woodstox:woodstox-core:' + versions.woodstox
    compile 'xerces:xercesImpl:' + versions.xerces
    compile 'xalan:xalan:' + versions.xalan
    testCompile 'junit:junit:' + versions.junit
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    integrationTestCompile "org.gebish:geb-junit4:" + versions.geb
    integrationTestCompile 'org.seleniumhq.selenium:selenium-chrome-driver:' + versions.selenium
    integrationTestCompile 'org.seleniumhq.selenium:selenium-firefox-driver:' + versions.selenium
    integrationTestCompile("com.codeborne:phantomjsdriver:1.2.1") {
        // phantomjs driver pulls in a different selenium version
        transitive = false
    }
    wagon 'org.apache.maven.wagon:wagon-ssh-external:2.10'
    // openssl native libs are optional and must be added to integration-test classpath separately
    if ('os x' == org.gradle.internal.os.OperatingSystem.current().getFamilyName()) {
        integrationTestCompile "io.netty:netty-tcnative:${versions.tcnative}:osx-x86_64"
    }
    if ('linux' == org.gradle.internal.os.OperatingSystem.current().getFamilyName()) {
        if (new File("/etc/redhat-release").exists()) {
            // use this for linking to libssl.so.10 (RHEL/Fedora/CentOS)
            integrationTestCompile "io.netty:netty-tcnative:${versions.tcnative}:linux-x86_64-fedora"
        } else {
            // use this for linking to libssl.so.1.0.0
            integrationTestCompile "io.netty:netty-tcnative:${versions.tcnative}:linux-x86_64"
        }
    }
    alpnboot "org.mortbay.jetty.alpn:alpn-boot:${versions.alpnboot}"
    distJars("org.elasticsearch:elasticsearch:${versions.elasticsearch}") {
        // dependencies that are overriden by org.xbib deps
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.dataformat'
        exclude group: 'com.google.guava'
        // dependencies that are not for client
        exclude module: 'org.elasticsearch:securesm'
        // we use log4j2, not log4j
        exclude group: 'log4j'
    }
    // Elasticsearch add-ons
    distJars 'com.vividsolutions:jts:1.13'
    distJars 'com.github.spullara.mustache.java:compiler:0.8.13'
    // indy
    distJars "org.codehaus.groovy:groovy-all:${versions.groovy}:indy"
    // exclude non-indy groovy (gradle flaw)
    distJars("org.xbib.groovy:groovy-webapp:${versions.'groovy-webapp'}") {
        exclude module: "org.codehaus.groovy:groovy-all:${versions.groovy}"
    }
    // netty 4.1 needs alpn for http/2
    distJars "org.mortbay.jetty.alpn:alpn-boot:${versions.alpnboot}"

}

compileGroovy {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}

test {
    testLogging {
        showStandardStreams = false
        exceptionFormat = 'full'
    }
}

task integrationTest(type: Test) {
    // note: bootstrapClasspath does not use /p (or /a)
    jvmArgs "-Xbootclasspath/p:" + configurations.alpnboot.asPath
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = configurations.integrationTestCompile
    classpath += sourceSets.main.output
    classpath += sourceSets.test.output
    classpath += sourceSets.integrationTest.output
    outputs.upToDateWhen { false }
    systemProperty 'log4j.configurationFile', projectDir.absolutePath + '/src/test/resources/log4j2.xml'
    systemProperty 'java.util.logging.manager','org.apache.logging.log4j.jul.LogManager'
    systemProperty 'geb.build.baseUrl', 'http://localhost:9502'
    systemProperty 'webapp.server', 'http://localhost:9502'
    systemProperty 'webapp.bind', 'http://localhost:9502'
    testLogging.showStandardStreams = true
    //systemProperty 'path.fonts', projectDir.absolutePath + '/src/main/webapp/'
}

drivers.each { driver ->
    task "${driver}IntegrationTest"(type: Test, dependsOn: ['unpackPlugin']) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = configurations.integrationTestCompile
        classpath += sourceSets.main.output
        classpath += sourceSets.test.output
        classpath += sourceSets.integrationTest.output
        outputs.upToDateWhen { false }
        systemProperty 'log4j.configurationFile', projectDir.absolutePath + '/src/test/resources/log4j2.xml'
        reports {
            junitXml.destination = file("$buildDir/test-results/${driver}")
        }
        systemProperty 'geb.build.reportsDir', reporting.file("${driver}/geb")
        systemProperty 'geb.env', driver
        testLogging.showStandardStreams = false
    }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

integrationTest.mustRunAfter test
check.dependsOn integrationTest

firefoxIntegrationTest {
    mustRunAfter test
}

chromeIntegrationTest {
    mustRunAfter test
    dependsOn unzipChromeDriver
    def chromedriverFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "chromedriver.exe" : "chromedriver"
    systemProperty "webdriver.chrome.driver", new File(unzipChromeDriver.outputs.files.singleFile, chromedriverFilename).absolutePath
}

phantomJsIntegrationTest {
    mustRunAfter test
    dependsOn unzipPhantomJs
    def phantomJsFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "phantomjs.exe" : "bin/phantomjs"
    systemProperty "phantomjs.binary.path", new File(unzipPhantomJs.outputs.files.singleFile, phantomJsFilename).absolutePath
}

clean {
    delete 'logs'
    delete 'sessions'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

shadowJar {
    baseName = project.name
    version = project.version
    classifier = 'standalone'
    manifest {
        attributes 'Main-Class': 'org.xbib.webapp.bootstrap.Bootstrap'
    }
}

artifacts {
    archives sourcesJar, shadowJar
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}

ext.grgit = org.ajoberstar.grgit.Grgit.open()

apply from: 'gradle/sonarqube.gradle'
apply from: 'gradle/linux.gradle'
apply from: 'gradle/git.gradle'
apply from: 'gradle/publish.gradle'


